import iaml;

extension metamodel::Extensions reexport;

pageTitle(model::InternetApplication this) :
	if name == null || name == "" then "(untitled application)" else name.toFirstUpper();

pageTitle(model::visual::Page this) :
	if name == null || name == "" then id else name;

formName(model::visual::InputForm this) :
	if (name == null || name == "") then "(untitled form)" else name;
	
textFieldName(model::visual::InputTextField this) :
	if (name == null || name == "") then "(untitled field)" else name;

buttonName(model::visual::Button this) :
	if (name == null || name == "") then "Submit Query" else name;
	
safeName(model::GeneratedElement this) :
	if (id == null) then
		throwException("Cannot get the safeName of an element with no ID: " + this)
	else
		safeNameString(id);

safeName(emf::EObject this) :
	throwException("Cannot get the safeName of an EObject: " + this);

safeNameString(String s) :
	s.replaceAll("[^A-Za-z0-9]", "_");

escapeString(String s) :
	s.replaceAll("[\"]", "\\\"");
	
/* TODO not a complete encoding, misses \r\n\t\v\f\[0-7]{1,3}\x[0-9A-Fa-f]{1,2} and perhaps unicode too */
escapePhpString(String s):
	s.replaceAll("[\\\\]", "\\\\").replaceAll("[\"]", "\\\"").replaceAll("[$]", "\\$");
	
operationName(model::WireEdgeDestination this) : 
	throwException("Invalid operation: " + this);

operationName(model::Operation this) :
	safeName(this);

conditionName(emf::EObject this ) :
	throwException("Invalid condition: " + this);

conditionName(model::Condition this) :
	safeName(this);
	
attributeName(model::DomainAttribute this) :
	safeNameString(name);

attributeName(model::DomainAttributeInstance this) :
	safeNameString(name);
	
attributeName(Void v) :
	throwException("Invalid attribute (Void)");

isXPath(model::DynamicApplicationElementSet this) :
	query.startsWith("xpath:");
	
/* is this operation contained in the current page? */
model::visual::Page containingPage(model::visual::Page element) :
	element;
	
model::visual::Page containingPage(model::NamedElement element) :
	containingPage(element.eContainer);

/* any page in the Session will contain all the operations for the Session */
model::visual::Page containingPage(model::scopes::Session element) :
	element.children.typeSelect( model::visual::Page ).first();

/* default for any EObject */
model::visual::Page containingPage(emf::EObject element) :
	null;

model::visual::Page containingPage(Void void) :
	null;

/* is [target] on the same page as [source]? */ 
onCurrentPage(model::WireEdgesSource source, model::WireEdgeDestination target) :
	containingPage(source) != null && 
	containingPage(source) == containingPage(target);

onCurrentPage(emf::EObject source, model::WireEdgeDestination target) :
	containingPage(source) != null && 
	containingPage(source) == containingPage(target);

/* is [target] in the same session as [source]? */ 
onCurrentSession(model::WireEdgesSource source, model::WireEdgeDestination target) :
	containingSession(source) != null && 
	containingSession(source) == containingSession(target);

/**
 * is [target] available in the same _script_ as [source]?
 * this is true if onCurrentPage() is true, or
 * it is an operation that will also be included in the current page.
 *
 * all operations contained within a session are also available to all
 * elements outside that session.
 *
 * this is based on allContainerOperations() and possibleParentOperations(). 
 */
availableInCurrentScript(model::WireEdgesSource source, model::WireEdgeDestination target) :
	onCurrentPage(source, target) ||
		containingSession(target) != null;

/**
 * Get all the operations that may be referenced in a container. In particular:
 * - Any operations in the page/container
 * - Any operations in the elements contained in this page/container
 * - Any operations contained in the direct parent scope (if any)
 */
allContainerOperations(model::ApplicationElementContainer this) :
	// we have to toSet() it so that we *clone* the list of operations, and not
	// add operations to the list directly
	// see http://www.openarchitectureware.org/forum/viewtopic.php?showtopic=11142
	operations.toSet().union( children.allContainerOperations() ).union( possibleParentOperations(eContainer) );

allContainerOperations(model::scopes::Session this) :
	operations.toSet().union( children.allContainerOperations() ).union( possibleParentOperations(eContainer) );

allContainerOperations(model::ApplicationElement this) :
	operations;

allContainerOperations(model::ContainsOperations this) :
	operations;

allContainerOperations(model::InternetApplication this) :
	 operations.toSet().union( sessions.allContainerOperations() ).union( children.allContainerOperations() );	 
		
/** All of the ApplicationElementProperties stored within this InternetApplication */
List[model::ApplicationElementProperty] allApplicationElementProperties(model::InternetApplication this) :
	properties.toSet().union( eAllContents.allApplicationElementProperties() ).reject( e | e == null );

List[model::ApplicationElementProperty] allApplicationElementProperties(emf::EObject this) :
	null;

List[model::ApplicationElementProperty] allApplicationElementProperties(model::ApplicationElementProperty this) :
	this;

/* for a page that is part of a session/scope */
possibleParentOperations(model::scopes::Session this ) :
	operations;

possibleParentOperations(emf::EObject this ) :
	{};

/* get the start node for an operation */
startNode(model::CompositeOperation this) :
	nodes.typeSelect(model::operations::StartNode).first();

startNodeCondition(model::CompositeCondition this) :
	nodes.typeSelect(model::operations::StartNode).first();

/* get the next execution flow for a decision operation that has passed */
passedExectionFlow(model::operations::DecisionOperation this) :	
	outExecutions.selectFirst( e | isPassFlow(e) );

/* get the next execution flow for a decision operation that has failed */
failedExectionFlow(model::operations::DecisionOperation this) :
	outExecutions.selectFirst( e | isFailFlow(e) );

hasFailFlow(model::operations::DecisionOperation this) :
	getFailFlows(this).size > 0; 
hasPassFlow(model::operations::DecisionOperation this) :
	getPassFlows(this).size > 0;
hasFailFlow(model::operations::DecisionCondition this) :
	getFailFlows(this).size > 0; 
hasPassFlow(model::operations::DecisionCondition this) :
	getPassFlows(this).size > 0;

isFailFlow(model::ExecutionEdge e) :
	e.name != null && (e.name.toLowerCase().startsWith("n") || e.name.toLowerCase().startsWith("f"));
isPassFlow(model::ExecutionEdge e) :
	!isFailFlow(e);

/* get failed/passed out edges from a given DecisionCondition */
getFailFlows(model::ExecutionEdgesSource this):
	outExecutions.select( e | isFailFlow(e) );

getPassFlows(model::ExecutionEdgesSource this):
	outExecutions.select( e | isPassFlow(e) );

/** does this operation have a "fail" edge? */
getFailEdge(model::CompositeOperation this) :
	outEdges.selectFirst( e | e.isFailEdge() ) ;

isFailEdge(model::WireEdge this ) :
	false;
	
isFailEdge(model::wires::CompositeWire this ) :
	name == "fail" ;

/** get the containing session of the given element, or null if none */
model::scopes::Session containingSession(emf::EObject this) :
	null;

model::scopes::Session containingSession(model::NamedElement this) :
	containingSession(eContainer);
	
model::scopes::Session containingSession(model::scopes::Session this) :
	this;

model::scopes::Session containingSession(model::InternetApplication this) :
	null;

model::DomainStore getDomainStore(model::DomainStore this) :
	this;
model::DomainStore getDomainStore(model::DomainObject this) :
	getDomainStore(eContainer);
model::DomainStore getDomainStore(model::DomainAttribute this) :
	getDomainStore(eContainer);
model::DomainStore getDomainStore(emf::EObject this) :
	null;

model::DomainObject getDomainObject(model::DomainObject this) :
	this;
model::DomainObject getDomainObject(model::DomainAttribute this) :
	getDomainObject(eContainer);
model::DomainObject getDomainObject(emf::EObject this) :
	null;
	
/** for a given setPropertyToValue operation, should we be calling
  additional event triggers? */
shouldExpandEventTriggers(model::PrimitiveOperation this ) :
	getOperationName( eContainer ) == "update";
	
shouldExpandEventTriggersServer(model::PrimitiveOperation this ) :
	shouldExpandEventTriggers(this);
	
getOperationName(model::Operation this) :
	name;
getOperationName(emf::EObject this) :
	throwException("Cannot get the operationName for an EObject: " + this);
	
isVisibleThing(emf::EObject this) :
	model::VisibleThing.isInstance(this);
isDomainAttribute(emf::EObject this) :
	model::DomainAttribute.isInstance(this);	
isDomainAttributeInstance(emf::EObject this) :
	model::DomainAttributeInstance.isInstance(this);
isDomainObjectInstance(emf::EObject this) :
	model::DomainObjectInstance.isInstance(this);
isSession(emf::EObject this) :
	model::scopes::Session.isInstance(this);	
isPage(emf::EObject this) :
	model::visual::Page.isInstance(this);	
isInputTextField(emf::EObject this) :
	model::visual::InputTextField.isInstance(this);	

isPropertiesFile(model::DomainStore store) :
	store != null && store.type != null && store.type.toString() == "PROPERTIES_FILE";

/* get the root InternetApplication */
model::InternetApplication getRoot(emf::EObject this) :
	getRoot(eContainer);

model::InternetApplication getRoot(model::InternetApplication this) :
	this;

/** get all Conditions in the InternetApplication */
cached List[model::Condition] getAllConditions(model::InternetApplication this) :
	eAllContents.typeSelect(model::Condition);

cached List[model::Condition] getAllConditions(emf::EObject this) :
	eAllContents.typeSelect(model::Condition);

cached List[model::Condition] getAllConditions(model::Condition this) :
	eAllContents.typeSelect(model::Condition).add(this);

/** get all Pages in the InternetApplication */
cached List[model::visual::Page] getAllPages(model::InternetApplication this) :
	children.typeSelect(model::visual::Page).toSet().union( eAllContents.getAllPages() ).reject( e | e == null );

cached List[model::visual::Page] getAllPages(emf::EObject this) :
	null;

cached List[model::visual::Page] getAllPages(model::visual::Page this) :
	this;
	
/** get all Dynamic Element Sets in the InternetApplication */
List[model::DynamicApplicationElementSet] getAllDynamicElements(model::InternetApplication this) :
	children.typeSelect( model::DynamicApplicationElementSet ).toSet()
		.union( eAllContents.getAllDynamicElements() ).reject( e | e == null );

List[model::DynamicApplicationElementSet] getAllDynamicElements(emf::EObject this) :
	null;

List[model::DynamicApplicationElementSet] getAllDynamicElements(model::DynamicApplicationElementSet this) :
	this;

/** get all Domain Attribute Instances in the InternetApplication */
List[model::DomainAttributeInstance] getAllDomainAttributeInstances(model::InternetApplication this) :
	eAllContents.typeSelect( model::DomainAttributeInstance );

List[model::DomainAttributeInstance] getAllDomainAttributeInstances(emf::EObject this) :
	eAllContents.typeSelect( model::DomainAttributeInstance );

List[model::DomainAttributeInstance] getAllDomainAttributeInstances(model::DomainAttributeInstance this) :
	eAllContents.typeSelect( model::DomainAttributeInstance ).add(this);

/** get all Domain Object Instances in the InternetApplication */
List[model::DomainObjectInstance] getAllDomainObjectInstances(model::InternetApplication this) :
	eAllContents.typeSelect(model::DomainObjectInstance);

List[model::DomainObjectInstance] getAllDomainObjectInstances(emf::EObject this) :
	eAllContents.typeSelect(model::DomainObjectInstance);

List[model::DomainObjectInstance] getAllDomainObjectInstances(model::DomainObjectInstance this) :
	eAllContents.typeSelect(model::DomainObjectInstance).add(this);

getAllDomainObjects(emf::EObject e) :
	e.eAllContents.typeSelect(model::DomainObject);

/** find all actual matches for the given DynamicApplicationSet in a given root */
List[emf::EObject] resolveDynamicSet(model::InternetApplication root, model::DynamicApplicationElementSet set) :
	JAVA org.openiaml.model.codegen.php.OawCodeGenerator.resolveDynamicSet(org.openiaml.model.model.InternetApplication, org.openiaml.model.model.DynamicApplicationElementSet);

/** get the fieldValue of the text field, or null if none exists. */
getFieldValue(model::visual::InputTextField this) :
	properties.selectFirst( e | e.name == "fieldValue" );

getFailHandler(model::WireEdgesSource this) :
	outEdges.select( w | w.isFailEdge()).first();

isAutosaveOff(model::DomainAttributeInstance this) :
	autosave == false || isAutosaveOff(eContainer);

isAutosaveOff(model::DomainObjectInstance this) :
	autosave == false;
	
isAutosaveOff(emf::EObject this):
	false;

model::DomainAttribute getPrimaryKey(model::DomainAttributeInstance this) :
	if inEdges.typeSelect(model::wires::NewInstanceWire).size >= 1 then
		getPrimaryKey(inEdges.typeSelect(model::wires::NewInstanceWire).first())
	else if model::DomainObjectInstance.isInstance(eContainer) then
		getPrimaryKey((model::DomainObjectInstance) eContainer);

model::DomainAttribute getPrimaryKey(model::wires::NewInstanceWire this) :
	getPrimaryKey(from);

model::DomainAttribute getPrimaryKey(model::DomainObject this) :
	attributes.selectFirst( e | e.primaryKey );

model::DomainAttribute getPrimaryKey(model::DomainAttribute this) :
	getPrimaryKey(eContainer);

model::DomainAttribute getPrimaryKey(model::DomainObjectInstance this) :
	getPrimaryKey(inEdges.typeSelect(model::wires::NewInstanceWire).first());

model::DomainAttribute getPrimaryKey(emf::EObject this) :
	null;

List[model::DomainObject] allSubtypes(model::DomainObject this):
	inEdges.typeSelect(model::wires::ExtendsWire).from.typeSelect(model::DomainObject).collect(e|allSubtypes(e)).flatten().add(this);

model::ExecutionEdgesSource castToExecutionEdgesSource(emf::EObject this) :
	(model::ExecutionEdgesSource) this;

/**
 * Get a list of all the possible execution states from the given node.
 * Cached to prevent loops causing infinite loops in logic.
 */
// for a destination that is actually a source: recurse
List[model::ExecutionEdgeDestination] allDestinations(model::ExecutionEdgesSource this, Collection[model::ExecutionEdgesSource] visited) :
	if visited.contains(this) then
		{}
	else
		outExecutions.to.toSet().addAll (
			outExecutions.to.collect(e | allDestinations(e, visited.toSet().add(this)) )
		).flatten()
	;

// default for an execution edge destination: empty
List[model::ExecutionEdgeDestination] allDestinations(Object this, Collection[model::ExecutionEdgesSource] visited) :
	{};

model::operations::JoinNode findFinalJoinNode(model::operations::SplitNode this) :
	findFinalJoinNode(outExecutions.to.selectFirst( t | findFinalJoinNode(t) != null));

model::operations::JoinNode findFinalJoinNode(emf::EObject this) :
	null;

model::operations::JoinNode findFinalJoinNode(model::ExecutionEdgesSource this) :
	findFinalJoinNode(outExecutions.to.selectFirst( t | findFinalJoinNode(t) != null));

model::operations::JoinNode findFinalJoinNode(model::operations::JoinNode this) :
	this;

/**
 * Get all necessary gates for the given scope, in order that they
 * need to be applied.
 */
List[model::components::EntryGate] getPageEntryGates(model::visual::Page this) :
	getPageEntryGates(eContainer);

List[model::components::EntryGate] getPageEntryGates(model::scopes::Session this) :
	if entryGate == null then
		getPageEntryGates(eContainer)
	else
		getPageEntryGates(eContainer).toSet().add( entryGate );

List[model::components::EntryGate] getPageEntryGates(emf::EObject this) :
	{};

/** Get all possible resume targets for this gate. */
getResumeTargets(model::components::Gate this) :
	eContainer.eContents.typeSelect(model::visual::Page);

/**
 * Get all operations which the server needs to keep track of, and 
 * the client needs to be updated.
 * <strong>All of these operations need to be consequence-free.</strong>
 */
List[model::Operation] getClientSideCacheOperations(model::InternetApplication this) :
	eAllContents.typeSelect(model::PrimitiveOperation)
		.select(o | model::DomainObjectInstance.isInstance(o.eContainer))
		.select(o | o.name == "exists?");

/** 
 * a hack way to create backtrace-able errors
 * based on http://www.openarchitectureware.org/forum/viewtopic.php?showtopic=5540
 * to use: «EXPAND exception FOR throwException("Your message goes here")»
 * AND add this anywhere in your template file: (very important)

«DEFINE exception FOR Object»
«ENDDEFINE»
   
 */
String throwException(String message) :
	JAVA org.openiaml.model.codegen.php.OawCodeGenerator.throwException(java.lang.String);

String _instrument(String destination, String filename, String location) :
	JAVA org.openiaml.model.codegen.php.coverage.InstrumentOawCode.instrument(java.lang.String, java.lang.String, java.lang.String);
