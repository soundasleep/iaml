import simplegmf;
import ecore;

extension metamodel::SimpleGMFExtensions;

context simplegmf::GMFConfiguration ERROR "RootElement must be set":
	rootElement != null;

context simplegmf::GMFConfiguration ERROR "id must be set to create id.gmfgraph":
	toString(id).length > 0;

context simplegmf::GMFConfiguration ERROR "There needs to be at exactly one default FigureConfiguration":
	figureConfigurations.select(fc|fc.isDefault).size == 1;

context simplegmf::FigureConfiguration ERROR "The default FigureConfiguration must have a shape set":
	if toBoolean(isDefault)
		then shape.toString() != "DEFAULT"
		else true;			

context simplegmf::LabelConfiguration ERROR "A LabelConfiguration needs to have a name set":
	toString(name).length > 0;

context simplegmf::DiagramEditor ERROR "A DiagramEditor must have an ID set":
	toString(id).length > 0;

context simplegmf::ReferencedNode ERROR "A ReferencedNode cannot have a containment feature set":
	containmentFeatures.isEmpty;
	
context simplegmf::ContainedEdge ERROR "Cannot derive the source meta feature for " + toString(classifier.name) + " in editor " + toString(editor.id) :
	deriveSourceMetaElement(getRoot(this), classifier) != null;

context simplegmf::ReferencedEdge ERROR "Cannot derive the source meta feature for " + toString(classifier.name) + " in editor " + toString(editor.id) :
	deriveSourceMetaElement(getRoot(this), classifier) != null;

context simplegmf::GMFConfiguration ERROR "Cannot derive the source meta feature for a referenced Edge Type: Move this edge type into Edge References":
	edgeTypes.forAll(c|deriveSourceMetaElement(c) != null);

context simplegmf::EdgeReference ERROR "Edge reference " + toString(classifier.name) + " must have a source feature set" :
	sourceFeature != null;

context simplegmf::EdgeReference ERROR "Edge reference " + toString(classifier.name) + " must have a target feature set" :
	targetFeature != null;

context simplegmf::EdgeReference ERROR "Edge reference " + toString(classifier.name) + " does not possess the source feature " + toString(sourceFeature.name) :
	classifier.eAllStructuralFeatures.contains(sourceFeature);

context simplegmf::EdgeReference ERROR "Edge reference " + toString(classifier.name) + " does not possess the target feature " + toString(targetFeature.name) :
	classifier.eAllStructuralFeatures.contains(targetFeature);

context simplegmf::DiagramElement ERROR "Containment feature not valid in diagram editor " + toString(editor.id) + ": " + toString(name) :
	containmentFeatures.forAll(f|isValidEObject(f));

context simplegmf::DiagramEditor ERROR "DiagramEditor " + toString(id) + " must have a root classifier set":
	classifier != null; 

context simplegmf::DiagramEditor ERROR "DiagramEditor " + toString(id) + " has an invalid contained DiagramElement" :
	elements.forAll(e|isValidEObject(e.classifier));
	
context simplegmf::LabelConfiguration ERROR "LabelConfiguration " + toString(name) + " has an invalid classifier":
	classifiers.forAll(c|isValidEObject(c));
