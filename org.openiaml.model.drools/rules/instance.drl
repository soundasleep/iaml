/**
 * Creates the default {@model Operation}s and {@model Condition}s for
 * {@model DomainIterator}s.
 */
package org.openiaml.model.drools.rules.instance

#list any import classes here.
import org.openiaml.model.drools.*;

import org.openiaml.model.inference.*;
import org.openiaml.model.model.*;
import org.openiaml.model.model.impl.*;
import org.openiaml.model.model.wires.*;
import org.openiaml.model.model.visual.*;
import org.openiaml.model.model.operations.*;
import org.openiaml.model.model.scopes.*;
import org.openiaml.model.model.components.*;
import org.openiaml.model.model.users.*;
import org.openiaml.model.model.domain.*;

#declare any global variables here
global OverridableCreateElementsHelper handler;
global DroolsInsertionQueue queue;
global DroolsHelperFunctions functions;

/**
 * @inference DomainIterator
 *		A {@model DomainIterator} has a {@model BuiltinOperation} 
 *		called '{@model DomainIterator#previous}'.
 */
rule "Create 'previous' BuiltinOperation"
	when
		instance : DomainIterator( )
		not ( op : Operation ( instance.previous == op ) )

		eval ( handler.veto( instance ))
	
	then
		BuiltinOperation op = handler.generatedBuiltinOperation(instance, instance, 
			DomainPackage.eINSTANCE.getDomainIterator_Previous());
		handler.setName(op, "previous");
		queue.add(op, drools);
	
end

/**
 * @inference DomainIterator
 *		A {@model DomainIterator} has a {@model BuiltinOperation} 
 *		called '{@model DomainIterator#next}'.
 */
rule "Create 'next' BuiltinOperation"
	when
		instance : DomainIterator( )
		not ( op : Operation ( instance.next == op ) )

		eval ( handler.veto( instance ))
	
	then
		BuiltinOperation op = handler.generatedBuiltinOperation(instance, instance, 
			DomainPackage.eINSTANCE.getDomainIterator_Next());
		handler.setName(op, "next");
		queue.add(op, drools);
	
end

/**
 * @inference DomainIterator
 *		A {@model DomainIterator} has a {@model BuiltinOperation} 
 *		called '{@model DomainIterator#reset}'.
 */
rule "Create 'reset' BuiltinOperation"
	when
		instance : DomainIterator( )
		not ( op : Operation ( instance.reset == op ) )

		eval ( handler.veto( instance ))
	
	then
		BuiltinOperation op = handler.generatedBuiltinOperation(instance, instance, 
			DomainPackage.eINSTANCE.getDomainIterator_Reset());
		handler.setName(op, "reset");
		queue.add(op, drools);
	
end

/**
 * @inference DomainIterator
 *		A {@model DomainIterator} has a {@model BuiltinOperation} 
 *		called '{@model DomainIterator#skip}'.
 */
rule "Create 'skip' BuiltinOperation"
	when
		instance : DomainIterator( )
		not ( op : Operation ( instance.skip == op ) )

		eval ( handler.veto( instance ))
	
	then
		BuiltinOperation op = handler.generatedBuiltinOperation(instance, instance, 
			DomainPackage.eINSTANCE.getDomainIterator_Skip());
		handler.setName(op, "skip");
		queue.add(op, drools);
	
end

/**
 * @inference DomainIterator
 *		A {@model DomainIterator} has a {@model BuiltinOperation} 
 *		called '{@model DomainIterator#jump}'.
 */
rule "Create 'jump' BuiltinOperation"
	when
		instance : DomainIterator( )
		not ( op : Operation ( instance.jump == op ) )

		eval ( handler.veto( instance ))
	
	then
		BuiltinOperation op = handler.generatedBuiltinOperation(instance, instance, 
			DomainPackage.eINSTANCE.getDomainIterator_Jump());
		handler.setName(op, "jump");
		queue.add(op, drools);
	
end

/**
 * @inference DomainIterator,BuiltinProperty
 *		A {@model DomainIterator} has a {@model BuiltinProperty} 
 *		called '{@model DomainIterator#hasNext}'.
 */
rule "Create 'hasNext' BuiltinProperty"
	when
		instance : DomainIterator( )

		type : EXSDDataType( definition != null,
			definition.name == "boolean" )
		
		not ( cond : Function ( instance.hasNext == cond ) )

		eval ( handler.veto( instance ))
	
	then
		BuiltinProperty cond = handler.generatedBuiltinProperty(instance, instance, 
			DomainPackage.eINSTANCE.getDomainIterator_HasNext());
		handler.setName(cond, "has next");
		handler.setType(cond, type);
		queue.add(cond, drools);
	
end

/**
 * @inference DomainIterator,BuiltinProperty
 *		A {@model DomainIterator} has a {@model BuiltinProperty} 
 *		called '{@model DomainIterator#hasPrevious}'.
 */
rule "Create 'hasPrevious' BuiltinProperty"
	when
		instance : DomainIterator( )

		type : EXSDDataType( definition != null,
			definition.name == "boolean" )

		not ( cond : Function ( instance.hasPrevious == cond ) )

		eval ( handler.veto( instance ))
	
	then
		BuiltinProperty cond = handler.generatedBuiltinProperty(instance, instance, 
			DomainPackage.eINSTANCE.getDomainIterator_HasPrevious());
		handler.setName(cond, "has previous");
		handler.setType(cond, type);
		queue.add(cond, drools);
	
end

/**
 * @inference DomainIterator,BuiltinProperty
 *		A {@model DomainIterator} has a {@model BuiltinProperty} 
 *		called '{@model DomainIterator#hasNext}'.
 */
rule "Create 'empty' BuiltinProperty"
	when
		instance : DomainIterator( )
		
		type : EXSDDataType( definition != null,
			definition.name == "boolean" )
		
		not ( cond : Function ( instance.empty == cond ) )

		eval ( handler.veto( instance ))
	
	then
		BuiltinProperty cond = handler.generatedBuiltinProperty(instance, instance, 
			DomainPackage.eINSTANCE.getDomainIterator_Empty());
		handler.setName(cond, "empty");
		handler.setType(cond, type);
		queue.add(cond, drools);
	
end

/**
 * @inference DomainIterator
 *		A {@model DomainIterator} has a {@model Value#readOnly} {@model Value} 
 *		called '{@model DomainIterator#results}'.
 */
rule "Create 'results' Property"
	when
		instance : DomainIterator( )
		not ( property : Value ( instance.results == property ) )

		eval ( handler.veto( instance ))
	
	then
		Value property = handler.generatedValue(instance, instance, 
			DomainPackage.eINSTANCE.getDomainIterator_Results());
		handler.setName(property, "results count");
		handler.setReadOnly(property, true);
		handler.setDefaultValue(property, "-1");
		queue.add(property, drools);
	
end

/**
 * @inference DomainIterator
 *		A {@model DomainIterator} has a {@model Event} 
 *		called '{@model DomainIterator#onIterate}'.
 */
rule "Create 'onIterate' Event"
	when
		instance : DomainIterator( )
		not ( event : Event ( instance.onIterate == event ) )

		eval ( handler.veto( instance ))
	
	then
		Event event = handler.generatedEvent(instance, instance, 
			DomainPackage.eINSTANCE.getDomainIterator_OnIterate());
		handler.setName(event, "on iterate");
		queue.add(event, drools);
	
end

/**
 * @inference DomainIterator
 *		A {@model DomainIterator} will contain a generated {@model Function}
 *		named 'not empty' - the inverse of the '{@model DomainIterator#empty}' condition.
 */
rule "Create 'not empty' composite condition"
	when
		instance : DomainIterator( )

		type : EXSDDataType( definition != null,
			definition.name == "boolean" )
		
		not ( condition : Function ( eContainer == instance, name == "not empty" ) )

		eval ( handler.veto( instance ))
	
	then
		ActivityFunction condition = handler.generatedActivityFunction(instance, instance);
		handler.setName(condition, "not empty");
		handler.setType(condition, type);
		queue.add(condition, drools);
	
end

/**
 * @inference DomainIterator,ActivityFunction
 *		The 'not empty' {@model ActivityFunction} for a {@model DomainIterator},
 *		will be completed with the {@model ActivityNode contents necessary} to check that the iterator is not {@model DomainIterator#empty}.
 */
rule "Create contents of 'not empty' condition"
	when
		instance : DomainIterator( )
		isEmpty : Function ( instance.empty == isEmpty )
		condition : ActivityFunction ( eContainer == instance, name == "not empty" )
		
		not ( StartNode ( eContainer == condition ))

		eval ( handler.veto( condition ))
	
	then
		StartNode start = handler.generatedStartNode(condition, condition);
		FinishNode finish = handler.generatedFinishNode(condition, condition);
		CancelNode cancel = handler.generatedCancelNode(condition, condition);
		queue.add(start, drools);
		queue.add(finish, drools);
		queue.add(cancel, drools);
		
		DecisionNode decision = handler.generatedDecisionNode(condition, condition);
		handler.setName(decision, "true?");
		queue.add(decision, drools);
		
		SimpleCondition edge = handler.generatedSimpleCondition(condition, condition, isEmpty, decision);
		queue.add(edge, drools);
		
		ExecutionEdge e1 = handler.generatedExecutionEdge(condition, condition, start, decision);
		ExecutionEdge e2 = handler.generatedExecutionEdge(condition, condition, decision, finish);
		handler.setName(e2, "n");
		ExecutionEdge e3 = handler.generatedExecutionEdge(condition, condition, decision, cancel);
		handler.setName(e3, "y");
		
		queue.add(e1, drools);
		queue.add(e2, drools);
		queue.add(e3, drools);
	
end
