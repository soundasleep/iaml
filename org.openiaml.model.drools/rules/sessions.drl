/**
 * Creates the 'onInit' {@model Event} for {@model Session}s.
 */
package org.openiaml.model.drools.rules.sessions

#list any import classes here.
import org.openiaml.model.drools.*;

import org.openiaml.model.inference.*;
import org.openiaml.model.model.*;
import org.openiaml.model.model.impl.*;
import org.openiaml.model.model.wires.*;
import org.openiaml.model.model.visual.*;
import org.openiaml.model.model.operations.*;
import org.openiaml.model.model.scopes.*;
import org.openiaml.model.model.components.*;

#declare any global variables here
global OverridableCreateElementsHelper handler;
global DroolsInsertionQueue queue;
global DroolsHelperFunctions functions;

/**
 * @inference Scope,Event
 *    A {@model Scope} contains an 'onInit' {@model Event}.
 */
rule "Create 'init' event for Sessions"
	when
		session : Scope( )
		not (event : Event( eContainer == session, session.onInit == event ))
				
		eval ( handler.veto( session ))
		
	then
		Event event = handler.generatedEventOnInit(session, session);
		queue.add(event, drools);
	
end

/**
 * If a Session contains an Operation, but not a Frame, then the target Operation
 * can never be called. Consequently, we add a new Frame to all empty Sessions
 * to permit these Operations to be called.
 *
 * <p>TODO This should NOT be put as part of the inference rules, but rather the
 * code generation logic - however, this is a quicker hack ;)
 *
 * @inference Session
 *		A {@model Session} will contain at least a single {@model Frame}.
 */
rule "Create an empty Frame for an empty Session, for Operation callbacks"
	when
		session : Session( )
		not ( Frame ( eContainer == session) )
				
		eval ( handler.veto( session ))
		
	then
		Frame frame = handler.generatedFrame(session, session);
		handler.setName(frame, "Empty frame for session");		
		queue.add(frame, drools);
	
end
