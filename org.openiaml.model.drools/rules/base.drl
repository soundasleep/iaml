#created on: 4/11/2008
package org.openiaml.model.drools

#list any import classes here.
import org.openiaml.model.inference.*;
import org.openiaml.model.model.*;
import org.openiaml.model.model.impl.*;
import org.openiaml.model.model.wires.*;
import org.openiaml.model.model.visual.*;
import org.openiaml.model.model.operations.*;
import org.openiaml.model.model.domain.*;

#declare any global variables here
global EcoreCreateElementsHelper handler;

rule "Create empty domain store"
		
	when
		#conditions
		a : InternetApplication ( name == "test" )
		not (DomainStore( name=="test domain store") from a.domainStores)
		// how do we know when not to create the domainstore?
	then 
		#actions
		DomainStore ds = (DomainStore) handler.createElement( a, ModelPackage.eINSTANCE.getDomainStore(), ModelPackage.eINSTANCE.getInternetApplication_DomainStores() );
		ds.setName("test domain store");
		ds.setIsGenerated(true);
		ds.setGeneratedBy(a);
		insert(ds); // possibly infer new knowledge (this is necessary)
		
end

rule "Create field value for input text field"
	when
		f : InputTextField( overridden == false )
		not (ApplicationElementProperty( eContainer == f, name=="fieldValue" ))
			
	then
		ApplicationElementProperty fieldValue = handler.generatedApplicationElementProperty(f, f);
		fieldValue.setName("fieldValue");
		insert(fieldValue);
		
end

rule "Create field value for domain attribute"
	when
		f : DomainAttribute( overridden == false )
		not (ApplicationElementProperty( eContainer == f, name=="fieldValue" ))
			
	then
		ApplicationElementProperty fieldValue = handler.generatedApplicationElementProperty(f, f);
		fieldValue.setName("fieldValue");
		insert(fieldValue);
		
end

rule "Empty forms connected by SyncWires create sync'd elements automatically"
	when
		wire : SyncWire( overridden == false )
		form : InputForm( overridden == false )
		domain : AbstractDomainObject( )
		attribute : AbstractDomainAttribute( eContainer == domain, attributeName : name )
		not (VisibleThing( eContainer == form, name == attributeName ))
		not (SyncWire( from == attribute, to.eContainer == form ))
		
	then
		InputTextField field = handler.generatedInputTextField(wire, form);
		field.setName(attribute.getName());
		insert(field);
		
		SyncWire wire2 = handler.generatedSyncWire(wire, form, attribute, field);
		wire2.setName("sync");
		insert(wire2);
		
end 


