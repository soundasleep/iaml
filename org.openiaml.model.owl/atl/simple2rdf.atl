
module simple2rdf;
create OUT : rdf from IN : simple;

helper def: namespaces: Map(String, String) =
	Map {
		('simple', 'http://openiaml.org/simple#'),
		('rdf', 'http://www.w3.org/1999/02/22-rdf-syntax-ns#'),
	 	('rdfs', 'http://www.w3.org/2000/01/rdf-schema#'),
	 	('owl', 'http://www.w3.org/2002/07/owl#'),
		('xsd', 'http://www.w3.org/2001/XMLSchema#')
	}->union( -- doesn't seem to do anything yet
		simple!EPackage.allInstancesFrom('IN')->iterate(it; 
			res: Map(String, String) = Map{} |
				res->including(it.name, it.getURI())
		)
	);

helper context simple!Page def: getURI() : String = 'http://www.example.org/page#' + self.name.trim();	

-- [http://wiki.eclipse.org/ATL/User_Guide#ATL_imperative_code]
-- helper def: counter : Integer = 0;
-- in imperative code: thisModule.counter <- thisModule.counter + 1;

entrypoint rule rdfGraph() {
	to
		r : rdf!RDFGraph (
			namespaces <- thisModule.namespaces.getKeys()->collect(e | thisModule.addNamespaces(e))
		)
	
	do {
		thisModule.graph <- r;
	}
}

lazy rule addNamespaces {
	from
		s : String
		
	to
		o : rdf!Namespace (
			prefix <- s,
			URI <- thisModule.namespaces.get(s)
		)
}

rule InternetApplication {
	from
		i : simple!InternetApplication
	
	to
		subject : rdf!Resource (
			URI <- 'http://openiaml.org/simple#root1',
			graph <- thisModule.graph
			-- subjectOf <- foo
			-- subjectOf <- i.pages->collect( p | thisModule.PageInInternetApplication(subject, p) )
		),
		t : rdf!Triple (
			subject <- subject,
			predicate <- predicate,
			object <- object,
			graph <- thisModule.graph
		),
		predicate : rdf!Property (
			URI <- 'http://www.w3.org/1999/02/22-rdf-syntax-ns#type',
			graph <- thisModule.graph
		),
		object : rdf!Resource (
			URI <- 'http://openiaml.org/simple#InternetApplication',
			graph <- thisModule.graph
		)
		-- foo : distinct rdf!Triple foreach ( p in i.pages) ( PageInInternetApplication(subject, p) )
		
}

rule PageInInternetApplication {
	from
		-- i : simple!InternetApplication,
		-- subject : rdf!Resource,
		p : simple!Page
		
	to
	
		subject : rdf!Resource (
			URI <- 'http://openiaml.org/simple#root1',
			graph <- thisModule.graph
		),	
		predicate : rdf!Property (
			URI <- 'http://openiaml.org/simple#pages',
			graph <- thisModule.graph
		),
		object : rdf!Resource (
			URI <- p.getURI(),
			graph <- thisModule.graph			
		),
		t : rdf!Triple (
			subject <- subject,
			predicate <- predicate,
			object <- object,
			graph <- thisModule.graph
		)
		
		do { 
			thisModule.PageType(p); 
			thisModule.PageName(p);
		}
}

-- "Note that a source model element of an ATL transformation should not be matched by more than one ATL matched rule"

lazy rule PageType {
	from
		p : simple!Page
		
	to
	
		subject : rdf!Resource (
			URI <- p.getURI(),
			graph <- thisModule.graph
		),
		predicate : rdf!Property (
			URI <- 'http://www.w3.org/1999/02/22-rdf-syntax-ns#type',
			graph <- thisModule.graph
		),
		object : rdf!Resource (
			URI <- 'http://openiaml.org/simple#Page',
			graph <- thisModule.graph
		),
		
		t : rdf!Triple (
			subject <- subject,
			predicate <- predicate,
			object <- object,
			graph <- thisModule.graph
		)
		
}

lazy rule PageName {
	from
		p : simple!Page
		
	to
	
		-- dataTypeURI : DataType
		-- DataType extends URIReference
		-- URIReference extends Node, URIElement
		-- Node (graph : Graph, objectOf : Triple)
		-- URIElement (URI : string)
	
		dataType : rdf!Datatype (
			URI <- 'http://www.w3.org/2001/XMLSchema#string',
			graph <- thisModule.graph
		),
		subject : rdf!Resource (
			URI <- p.getURI(),
			graph <- thisModule.graph
		),
		predicate : rdf!Property (
			URI <- 'http://openiaml.org/simple#name',
			graph <- thisModule.graph
		),
		object : rdf!TypedLiteral (
			lexicalForm <- p.name,
			datatypeURI <- dataType,
			graph <- thisModule.graph
		),
		
		t : rdf!Triple (
			subject <- subject,
			predicate <- predicate,
			object <- object,
			graph <- thisModule.graph
		)
		
}
