#created on: 4/11/2008
package org.openiaml.verification.drools.rules.accessible;

#list any import classes here.
import org.openiaml.verification.drools.*;

import org.openiaml.model.model.*;
import org.openiaml.model.model.visual.*;
import org.openiaml.model.model.wires.*;
import org.openiaml.verification.model.validation.*;

global org.openiaml.verification.model.validation.ValidationFactory factory;
global org.openiaml.verification.drools.VerifyHandler verify;

rule "Frame has a path (1)"
	when
		p : Frame ( )
		b : Button ( eContainer == p )
		p2 : Frame ( )
		n : ActionEdge ( from == b, to == p2, name == "onClick")
		not ( PathTo ( from == p, to == p2 ))
	
	then
		PathTo path = factory.createPathTo();
		path.setFrom(p);
		path.setTo(p2);
		insert(path);

end
		
rule "Frame has a path (2)"
	when
		p : Frame ( )
		b : Button ( eContainer == p )
		e : EventTrigger ( eContainer == b, name == "click" )
		p2 : Frame ( )
		n : ActionEdge ( from == e, to == p2 )
		not ( PathTo ( from == p, to == p2 ))
	
	then
		PathTo path = factory.createPathTo();
		path.setFrom(p);
		path.setTo(p2);
		insert(path);

end

rule "Recursiveness of PathTo"
	when
		p1 : Frame ( )
		p2 : Frame ( )
		p3 : Frame ( )
		PathTo ( from == p1, to == p2 )
		PathTo ( from == p2, to == p3 )
		not ( PathTo ( from == p1, to == p3 ))
	
	then
		PathTo path = factory.createPathTo();
		path.setFrom(p1);
		path.setTo(p3);
		insert(path);
	
end

rule "Frame is accessible"
	when
		home : Frame ( name == "Home" )
		p : Frame ( eval(p != home) )
		not ( PathTo ( from == home, to == p ))
	
	then
		Violation violation = factory.createViolation();
		violation.setSource(p);
		violation.setReason("Frame is not accessible");
		verify.failed(violation);
		insert(violation);

end

rule "Retract previously inaccessible frame"
	when
		home : Frame ( name == "Home" )
		p : Frame ( )
		violation : Violation ( source == p, reason == "Frame is not accessible" )
		PathTo ( from == home, to == p )
	
	then
		# retract
		verify.retract(violation);
		retract(violation);

end
