#created on: 4/11/2008
package org.openiaml.model.drools

#list any import classes here.
import org.openiaml.model.inference.*;
import org.openiaml.model.model.*;
import org.openiaml.model.model.impl.*;
import org.openiaml.model.model.wires.*;
import org.openiaml.model.model.visual.*;
import org.openiaml.model.model.operations.*;
import org.openiaml.model.model.scopes.*;
import org.eclipse.emf.ecore.*;
import java.util.*;

#declare any global variables here
global EcoreCreateElementsHelper handler;

# insertion queue
import org.openiaml.model.drools.*;
global DroolsInsertionQueue queue;

# TODO put into separate file
function boolean syncMatches(NamedElement e1, NamedElement e2) {
	return e1.getName().toLowerCase().equals(e2.getName().toLowerCase());
}

# TODO put into separate file
function boolean connects(WireEdge wire, Object a, Object b) {
	if (wire.getFrom() == null)
		throw new NullPointerException("Wire '" + wire + "'.from = null");
	if (wire.getTo() == null)
		throw new NullPointerException("Wire '" + wire + "'.to = null");
	return (wire.getFrom().equals(a) && wire.getTo().equals(b)) ||
		(wire.getFrom().equals(b) && wire.getTo().equals(a));
}

# TODO put into separate file
function Session containingSession(EObject e) {
	if (e.eContainer() == null) {
		return null;
	}
	if (e.eContainer() instanceof Session) {
		return (Session) e.eContainer();
	}
	return containingSession(e.eContainer());
}

function boolean containingSessionEquals(EObject e, Session s) {
	Session actual = containingSession(e);
	return s.equals(actual);
}

rule "Sync wires between objects should create sync wires between all elements inside each object that matches"
	when
		# source : SyncWire(overridden == false, eval(connects(source, p1, p2)) )
		source : SyncWire(overridden == false)
		p1 : ApplicationElement( ) from source.from
		p2 : ApplicationElement(this != p1) from source.to
		
		e1 : ApplicationElement( eContainer == p1 )
		e2 : ApplicationElement( eContainer == p2, eval(syncMatches(e1, e2)) )
	
		not (sw : SyncWire( name == "sync", eContainer == source, eval(connects( sw, e1, e2)) ))
		
	then
		SyncWire sw = handler.generatedSyncWire(source, source, e1, e2);
		handler.setName(sw, "sync");
		queue.add(sw, drools);
		
end

rule "Run instance wire from edit to update"
	when
		sw : SyncWire(overridden == false)
		source : ContainsEventTriggers( )
		target : ContainsOperations( )
		eval(connects(sw, source, target))
		
		event : EventTrigger( eContainer == source, name=="edit" )
		operation : Operation( eContainer == target, name=="update" )
		
		not (RunInstanceWire( eContainer == sw, from == event, to == operation, name == "run" ))
		
	then
		RunInstanceWire rw = handler.generatedRunInstanceWire(sw, sw, event, operation);
		handler.setName(rw, "run");
		queue.add(rw, drools);

end

# Ideally, these two commands would be joined together into one
rule "Connect parameter wire to: run instance wire from source.edit to target.update"
	when
		sw : SyncWire(overridden == false)
		source : InputTextField( ) from sw.from
		target : InputTextField( ) from sw.to
	
		event : EventTrigger( eContainer == source, name=="edit" )
		operation : Operation( eContainer == target, name=="update" )
		field : ApplicationElementProperty( eContainer == source, name=="fieldValue" )
		wire : RunInstanceWire( from == event, to == operation, name == "run", eContainer == sw )
		
		not (ParameterWire( eContainer == sw, from == field, to == wire )) 
		
	then
		ParameterWire pw = handler.generatedParameterWire(sw, sw, field, wire); 
		handler.setName(pw, "[sync] run instance parameter (source.edit)"); 
		queue.add(pw, drools);

end

rule "Connect parameter wire to: run instance wire from target.edit to source.update"
	when
		sw : SyncWire(overridden == false)
		target : InputTextField( ) from sw.from
		source : InputTextField( ) from sw.to
	
		event : EventTrigger( eContainer == source, name=="edit" )
		operation : Operation( eContainer == target, name=="update" )
		field : ApplicationElementProperty( eContainer == source, name=="fieldValue" )
		wire : RunInstanceWire( from == event, to == operation, name == "run", eContainer == sw )
		
		not (ParameterWire( eContainer == sw, from == field, to == wire )) 
		
	then
		ParameterWire pw = handler.generatedParameterWire(sw, sw, field, wire); 
		handler.setName(pw, "[sync] run instance parameter (target.edit)"); 
		queue.add(pw, drools);

end

# Ideally, these two commands would be joined together into one
rule "Run instance wire from source.access to source.init, when we have a value to provide as a parameter"
	when
		sw : SyncWire(overridden == false)
		source : ContainsEventTriggers( ) from sw.from  
		target : ContainsOperations( ) from sw.to
		
		event : EventTrigger( eContainer == source, name=="access" )
		operation : Operation( eContainer == source, name=="init" )
		field : ApplicationElementProperty( eContainer == target, name=="fieldValue" )
		
		not (RunInstanceWire( eContainer == sw, from == event, to == operation, name == "run" ))
		
	then
		RunInstanceWire rw = handler.generatedRunInstanceWire(sw, sw, event, operation);
		handler.setName(rw, "run");
		queue.add(rw, drools);

end

rule "Run instance wire from target.access to target.init, when we have a value to provide as a parameter"
	when
		sw : SyncWire(overridden == false)
		source : ContainsEventTriggers( ) from sw.from  
		target : ContainsOperations( ) from sw.to
		
		event : EventTrigger( eContainer == target, name=="access" )
		operation : Operation( eContainer == target, name=="init" )
		field : ApplicationElementProperty( eContainer == source, name=="fieldValue" )
		
		not (RunInstanceWire( eContainer == sw, from == event, to == operation, name == "run" ))
		
	then
		RunInstanceWire rw = handler.generatedRunInstanceWire(sw, sw, event, operation);
		handler.setName(rw, "run");
		queue.add(rw, drools);

end

# Ideally, these two commands would be joined together into one
rule "Connect parameter wire to: run instance wire from source.access to source.init"
	when
		sw : SyncWire(overridden == false)
		source : InputTextField( ) from sw.from
		target : InputTextField( ) from sw.to
	
		event : EventTrigger( eContainer == source, name=="access" )
		operation : Operation( eContainer == source, name=="init" )
		field : ApplicationElementProperty( eContainer == target, name=="fieldValue" )
		wire : RunInstanceWire( from == event, to == operation, name == "run", eContainer == sw )
		
		not (ParameterWire( eContainer == sw, from == field, to == wire )) 
		
	then
		ParameterWire pw = handler.generatedParameterWire(sw, sw, field, wire); 
		handler.setName(pw, "[sync] run instance parameter (source.access)"); 
		queue.add(pw, drools);

end

rule "Connect parameter wire to: run instance wire from target.access to target.init"
	when
		sw : SyncWire(overridden == false)
		source : InputTextField( ) from sw.from
		target : InputTextField( ) from sw.to
	
		event : EventTrigger( eContainer == target, name=="access" )
		operation : Operation( eContainer == target, name=="init" )
		field : ApplicationElementProperty( eContainer == source, name=="fieldValue" )
		wire : RunInstanceWire( from == event, to == operation, name == "run", eContainer == sw )
		
		not (ParameterWire( eContainer == sw, from == field, to == wire )) 
		
	then
		ParameterWire pw = handler.generatedParameterWire(sw, sw, field, wire);
		handler.setName(pw, "[sync] run instance parameter (target.access)"); 
		queue.add(pw, drools);

end

/**
 * I think that chained SyncWires are generally impossible. Consider:
 * 
 *   page1 <--> page2 <--> unrelated
 *   condition on both SyncWires, matching (page1, page2)
 * 
 * If we allow for chaining, then the condition 
 *   condition[page2, unrelated]
 * will be copied onto
 *   condition[page1, page2]
 * which means the SyncWire will never satisfy all its conditions.
 *
 * --
 *
 * I think that chained SyncWires are only possible *when* each
 * SyncWire has identical Conditions, so we don't have to combine
 * the two conditions together into something impossible.
 *
 * However, trying to implement this did nothing. I think it is because
 * without adding more information to the model, you cannot chain.
 * The information you'd be adding is stuff like ChainedSyncWire, but
 * this defeats the purpose of having it in the model, because this should
 * be handled by the code generation instead.
 */

rule "Cascaded SyncWires: Conditions from parent to child"
	when

		# the containing elements	
		sw : SyncWire ( overridden == false )
		source : ApplicationElement( ) from sw.from  
		target : ApplicationElement( ) from sw.to
		
		# the elements contained
		e1 : ApplicationElement( eContainer == source )
		e2 : ApplicationElement( eContainer == target, eval(syncMatches(e1, e2)) )
		esw : SyncWire( name == "sync", eval(connects(esw, e1, e2)), overridden == false )
		
		# there is a condition on the containing sync wire
		condition : Condition ( )
		cw : ConditionWire( from == condition, to == sw )

		# and there isn't one on the element sync wire
		not (ecw : ConditionWire( generatedBy == cw, from == condition, to == esw )) 
		
	then
		ConditionWire ecw = handler.generatedConditionWire( cw, cw, condition, esw );
		handler.setName(ecw, "[copied] from: " + cw.getId());
		queue.add(ecw, drools);

end

rule "Cascaded SyncWires: Parameters from parent to child"
	when

		# the containing elements
		sw : SyncWire ( overridden == false )
		source : ApplicationElement( ) from sw.from  
		target : ApplicationElement( ) from sw.to
		
		# the elements contained
		e1 : ApplicationElement( eContainer == source )
		e2 : ApplicationElement( eContainer == target, eval(syncMatches(e1, e2)) )
		esw : SyncWire( name == "sync", eval(connects(esw, e1, e2)), overridden == false )
		
		# there is a condition on the containing sync wire
		condition : Condition ( )
		cw : ConditionWire( from == condition, to == sw )
		
		# the generated condition wire
		ecw : ConditionWire( generatedBy == cw, from == condition, to == esw )
		
		# there is a parameter on the condition wire
		parameter : WireEdgesSource( )
		pw : ParameterWire( from == parameter, to == cw )

		# and there isn't one on the element sync wire
		not (epw : ParameterWire( generatedBy == cw, from == parameter, to == ecw )) 
		
	then
		# create it
		ParameterWire epw = handler.generatedParameterWire( cw, cw, parameter, ecw );
		handler.setName(epw, "[copied] from: " + pw.getId());
		queue.add(epw, drools);

end

# SyncWires do not connect the fields together; they can _also_
# connect the Pages together.
rule "Connect Session.init to incoming SyncWires (contained in Pages)"
	when
		session : Session ( overridden == false )
		sw : SyncWire( overridden == false )
		sourcePage : ContainsEventTriggers( eval(!containingSessionEquals(sourcePage, session)) )  
		targetPage : ContainsOperations( eval(containingSessionEquals(targetPage, session)) )
		eval( connects(sw, sourcePage, targetPage ))

		sw2 : SyncWire ( eContainer == sw, overridden == false)
		sourceElement : ContainsEventTriggers( eContainer == sourcePage )
		targetElement : ContainsOperations( eContainer == targetPage )
		eval( connects(sw2, sourceElement, targetElement ))

		event : EventTrigger( eContainer == sourceElement, name == "edit" )
		operation : Operation( eContainer == targetElement, name == "update" )

		rw : RunInstanceWire( eContainer == sw2, from == event, to == operation, name == "run" )
		inite : EventTrigger (eContainer == session, name == "init" )
		
		not (RunInstanceWire( eContainer == sw2, from == inite, to == operation, name == "run" ))
		
	then
		RunInstanceWire rw2 = handler.generatedRunInstanceWire(sw2, sw2, inite, operation);
		handler.setName(rw2, "run");
		queue.add(rw2, drools);
		
end

rule "Connect parameter for Session.init to incoming SyncWires (contained in Pages)"
	when
		session : Session ( overridden == false )
		sw : SyncWire( overridden == false )
		sourcePage : ContainsEventTriggers( eval(!containingSessionEquals(sourcePage, session)) )  
		targetPage : ContainsOperations( eval(containingSessionEquals(targetPage, session)) )
		eval( connects(sw, sourcePage, targetPage ))

		sw2 : SyncWire ( eContainer == sw, overridden == false)
		sourceElement : ContainsEventTriggers( eContainer == sourcePage )
		targetElement : ContainsOperations( eContainer == targetPage )
		eval( connects(sw2, sourceElement, targetElement ))

		event : EventTrigger( eContainer == sourceElement, name == "edit" )
		operation : Operation( eContainer == targetElement, name == "update" )

		rw : RunInstanceWire( eContainer == sw2, from == event, to == operation, name == "run" )
		inite : EventTrigger (eContainer == session, name == "init" )

		initrw : RunInstanceWire( eContainer == sw2, from == inite, to == operation, name == "run" )
		
		field : ApplicationElementProperty( eContainer == sourceElement, name=="fieldValue" )
		pw : ParameterWire( eContainer == sw2, from == field, to == rw ) 

		not( ParameterWire( eContainer == sw2, from == field, to == initrw ))  
		
	then
		ParameterWire pw2 = handler.generatedParameterWire(sw, sw, field, initrw);
		queue.add(pw2, drools);		
end


rule "Refresh New Instance Object mappings when sync connected to Forms: create text fields"
	when
		sync : SyncWire( overridden == false )
		form : InputForm ( overridden == false )
		instance : DomainObjectInstance ( overridden == false )
		
		eval(connects(sync, form, instance))
		
		attribute : DomainAttributeInstance( eContainer == instance )
		
		not ( tf : InputTextField( eval(syncMatches( attribute, tf )) ) )
		
	then
		# create a new one
		InputTextField text = handler.generatedInputTextField(sync, form);
		handler.setName(text, attribute.getName());
		queue.add(text, drools);

end
