import iaml;

extension metamodel::Extensions reexport;

pageTitle(model::InternetApplication this) :
	if name == null || name == "" then "(untitled application)" else name.toFirstUpper();

pageTitle(model::visual::Page this) :
	if name == null || name == "" then id else name;

formName(model::visual::InputForm this) :
	if (name == null || name == "") then "(untitled form)" else name;
	
textFieldName(model::visual::InputTextField this) :
	if (name == null || name == "") then "(untitled field)" else name;

buttonName(model::visual::Button this) :
	if (name == null || name == "") then "Submit Query" else name;
	
safeName(model::GeneratedElement this) :
	safeNameString(id);

safeName(emf::EObject this) :
	"unexpected eobject " + this;

safeNameString(String s) :
	s.replaceAll("[^A-Za-z0-9]", "_");

escapeString(String s) :
	s.replaceAll("[\"]", "\\\"");
	
/* TODO not a complete encoding, misses \r\n\t\v\f\[0-7]{1,3}\x[0-9A-Fa-f]{1,2} and perhaps unicode too */
escapePhpString(String s):
	s.replaceAll("[\\\\]", "\\\\").replaceAll("[\"]", "\\\"").replaceAll("[$]", "\\$");
	
operationName(model::WireEdgeDestination this) : 
	"invalid_operation";

operationName(model::Operation this) :
	safeName(this);

conditionName(emf::EObject this ) :
	"invalid_condition";

conditionName(model::Condition this) :
	safeName(this);
	
attributeName(model::DomainAttribute this) :
	safeNameString(name);

attributeName(model::DomainAttributeInstance this) :
	safeNameString(name);
	
attributeName(Void v) :
	"null";

isXPath(model::DynamicApplicationElementSet this) :
	query.startsWith("xpath:");
	
/* is this operation contained in the current page? */
model::visual::Page containingPage(model::visual::Page element) :
	element;
	
model::visual::Page containingPage(model::NamedElement element) :
	containingPage(element.eContainer);

/* any page in the Session will contain all the operations for the Session */
model::visual::Page containingPage(model::scopes::Session element) :
	element.children.typeSelect( model::visual::Page ).first();

/* default for any EObject */
model::visual::Page containingPage(emf::EObject element) :
	null;

model::visual::Page containingPage(Void void) :
	null;

/* is [target] on the same page as [source]? */ 
onCurrentPage(model::WireEdgesSource source, model::WireEdgeDestination target) :
	containingPage(source) != null && 
	containingPage(source) == containingPage(target);

onCurrentPage(emf::EObject source, model::WireEdgeDestination target) :
	containingPage(source) != null && 
	containingPage(source) == containingPage(target);

/* is [target] in the same session as [source]? */ 
onCurrentSession(model::WireEdgesSource source, model::WireEdgeDestination target) :
	containingSession(source) != null && 
	containingSession(source) == containingSession(target);

/**
 * is [target] available in the same _script_ as [source]?
 * this is true if onCurrentPage() is true, or
 * it is an operation that will also be included in the current page.
 *
 * all operations contained within a session are also available to all
 * elements outside that session.
 *
 * this is based on allContainerOperations() and possibleParentOperations(). 
 */
availableInCurrentScript(model::WireEdgesSource source, model::WireEdgeDestination target) :
	onCurrentPage(source, target) ||
		containingSession(target) != null;

/**
 * Get all the operations that may be referenced in a container. In particular:
 * - Any operations in the page/container
 * - Any operations in the elements contained in this page/container
 * - Any operations contained in the direct parent scope (if any)
 */
allContainerOperations(model::ApplicationElementContainer this) :
	// we have to toSet() it so that we *clone* the list of operations, and not
	// add operations to the list directly
	// see http://www.openarchitectureware.org/forum/viewtopic.php?showtopic=11142 
	operations.toSet().addAll( children.allContainerOperations() ).addAll( possibleParentOperations(eContainer) );

allContainerOperations(model::ApplicationElement this) :
	operations;

allContainerOperations(model::ContainsOperations this) :
	operations;

allContainerOperations(model::InternetApplication this) :
	 operations.toSet().addAll( sessions.allContainerOperations() ).addAll( children.allContainerOperations() );	 
		
/** All of the ApplicationElementProperties stored within this InternetApplication */
List[model::ApplicationElementProperty] allApplicationElementProperties(model::InternetApplication this) :
	properties.toSet().union( eAllContents.allApplicationElementProperties() ).reject( e | e == null );

List[model::ApplicationElementProperty] allApplicationElementProperties(emf::EObject this) :
	null;

List[model::ApplicationElementProperty] allApplicationElementProperties(model::ApplicationElementProperty this) :
	this;

/* for a page that is part of a session/scope */
possibleParentOperations(model::scopes::Session this ) :
	operations;

possibleParentOperations(emf::EObject this ) :
	{};

/* get the start node for an operation */
startNode(model::CompositeOperation this) :
	nodes.selectFirst( node | node.metaType.isAssignableFrom(model::operations::StartNode) );

startNodeCondition(model::CompositeCondition this) :
	nodes.selectFirst( node | node.metaType.isAssignableFrom(model::operations::StartNode) );

/* get the next execution flow for a decision operation that has passed */
passedExectionFlow(model::operations::DecisionOperation this) :
	outExecutions.selectFirst( flow | !flow.to.metaType.isAssignableFrom(model::operations::CancelNode) );

failedExectionFlow(model::operations::DecisionOperation this) :
	outExecutions.selectFirst( flow | flow.to.metaType.isAssignableFrom(model::operations::CancelNode) );

/** does this operation have a "fail" edge? */
getFailEdge(model::CompositeOperation this) :
	outEdges.selectFirst( e | e.isFailEdge() ) ;

isFailEdge(model::WireEdge this ) :
	false;
	
isFailEdge(model::wires::CompositeWire this ) :
	name == "fail" ;

/** get the containing session of the given element, or null if none */
model::scopes::Session containingSession(emf::EObject this) :
	null;

model::scopes::Session containingSession(model::NamedElement this) :
	containingSession(eContainer);
	
model::scopes::Session containingSession(model::scopes::Session this) :
	this;

model::scopes::Session containingSession(model::InternetApplication this) :
	null;

model::DomainStore getDomainStore(model::DomainStore this) :
	this;
model::DomainStore getDomainStore(model::DomainObject this) :
	getDomainStore(eContainer);
model::DomainStore getDomainStore(model::DomainAttribute this) :
	getDomainStore(eContainer);
model::DomainStore getDomainStore(emf::EObject this) :
	null;

model::DomainObject getDomainObject(model::DomainObject this) :
	this;
model::DomainObject getDomainObject(model::DomainAttribute this) :
	getDomainObject(eContainer);
model::DomainObject getDomainObject(emf::EObject this) :
	null;
	
/** for a given setPropertyToValue operation, should we be calling
  additional event triggers? */
shouldExpandEventTriggers(model::ChainedOperation this ) :
	getOperationName( eContainer ) == "update";
	
shouldExpandEventTriggersServer(model::ChainedOperation this ) :
	shouldExpandEventTriggers(this);
	
getOperationName(model::Operation this) :
	name;
getOperationName(emf::EObject this) :
	"[no operation name for an EObject]";
	
isVisibleThing(emf::EObject this) :
	model::VisibleThing.isInstance(this);
isDomainAttribute(emf::EObject this) :
	model::DomainAttribute.isInstance(this);	
isDomainAttributeInstance(emf::EObject this) :
	model::DomainAttributeInstance.isInstance(this);
isDomainObjectInstance(emf::EObject this) :
	model::DomainObjectInstance.isInstance(this);
isSession(emf::EObject this) :
	model::scopes::Session.isInstance(this);	
isPage(emf::EObject this) :
	model::visual::Page.isInstance(this);	
isInputTextField(emf::EObject this) :
	model::visual::InputTextField.isInstance(this);	

isPropertiesFile(model::DomainStore store) :
	store != null && store.type != null && store.type.toString() == "PROPERTIES_FILE";

/* get failed/passed out edges from a given DecisionCondition */
getFailEdges(model::operations::DecisionCondition this):
	outExecutions.select( e | model::operations::CancelNode.isInstance(e.to) );
	/* TODO one day, this will also include OutExecutions[name=cancel|fail|N] */

getFailEdges(model::operations::OperationCallNode this):
	outExecutions.select( e | model::operations::CancelNode.isInstance(e.to) );

getPassEdges(model::operations::DecisionCondition this):
	outExecutions.toSet().removeAll( getFailEdges() );	/* everything that didn't fail */	

getPassEdges(model::operations::OperationCallNode this):
	outExecutions.toSet().removeAll( getFailEdges() );	/* everything that didn't fail */	

/* get the root InternetApplication */
model::InternetApplication getRoot(emf::EObject this) :
	getRoot(eContainer);

model::InternetApplication getRoot(model::InternetApplication this) :
	this;

/** get all Conditions in the InternetApplication */
cached List[model::Condition] getAllConditions(model::InternetApplication this) :
	conditions.toSet().union( eAllContents.getAllConditions() ).reject( e | e == null );

cached List[model::Condition] getAllConditions(emf::EObject this) :
	null;

cached List[model::Condition] getAllConditions(model::Condition this) :
	this;

/** get all Pages in the InternetApplication */
cached List[model::visual::Page] getAllPages(model::InternetApplication this) :
	children.typeSelect(model::visual::Page).toSet().union( eAllContents.getAllPages() ).reject( e | e == null );

cached List[model::visual::Page] getAllPages(emf::EObject this) :
	null;

cached List[model::visual::Page] getAllPages(model::visual::Page this) :
	this;
	
/** get all Dynamic Element Sets in the InternetApplication */
List[model::DynamicApplicationElementSet] getAllDynamicElements(model::InternetApplication this) :
	children.typeSelect( model::DynamicApplicationElementSet ).toSet()
		.union( eAllContents.getAllDynamicElements() ).reject( e | e == null );

List[model::DynamicApplicationElementSet] getAllDynamicElements(emf::EObject this) :
	null;

List[model::DynamicApplicationElementSet] getAllDynamicElements(model::DynamicApplicationElementSet this) :
	this;

/** get all Domain Attribute Instances in the InternetApplication */
List[model::DomainAttributeInstance] getAllDomainAttributeInstances(model::InternetApplication this) :
	children.typeSelect( model::DomainAttributeInstance ).toSet()
		.union( eAllContents.getAllDomainAttributeInstances() ).reject( e | e == null );

List[model::DomainAttributeInstance] getAllDomainAttributeInstances(emf::EObject this) :
	null;

List[model::DomainAttributeInstance] getAllDomainAttributeInstances(model::DomainAttributeInstance this) :
	this;

/** get all Domain Object Instances in the InternetApplication */
List[model::DomainObjectInstance] getAllDomainObjectInstances(model::InternetApplication this) :
	children.typeSelect( model::DomainObjectInstance ).toSet()
		.union( eAllContents.getAllDomainObjectInstances() ).reject( e | e == null );

List[model::DomainObjectInstance] getAllDomainObjectInstances(emf::EObject this) :
	null;

List[model::DomainObjectInstance] getAllDomainObjectInstances(model::DomainObjectInstance this) :
	this;

/** find all actual matches for the given DynamicApplicationSet in a given root */
List[emf::EObject] resolveDynamicSet(model::InternetApplication root, model::DynamicApplicationElementSet set) :
	JAVA org.openiaml.model.codegen.oaw.OawCodeGenerator.resolveDynamicSet(org.openiaml.model.model.InternetApplication, org.openiaml.model.model.DynamicApplicationElementSet);

/** get the fieldValue of the text field, or null if none exists. */
getFieldValue(model::visual::InputTextField this) :
	properties.selectFirst( e | e.name == "fieldValue" );

getFailHandler(model::WireEdgesSource this) :
	outEdges.select( w | w.isFailEdge()).first();

isAutosaveOff(model::DomainAttributeInstance this) :
	autosave == false || isAutosaveOff(eContainer);

isAutosaveOff(model::DomainObjectInstance this) :
	autosave == false;
	
isAutosaveOff(emf::EObject this):
	false;

getPrimaryKey(model::DomainAttributeInstance this) :
	if inEdges.typeSelect(model::wires::NewInstanceWire).size >= 1 then
		getPrimaryKey(inEdges.typeSelect(model::wires::NewInstanceWire).first())
	else if eContainer.metaType.isAssignableFrom(model::DomainObjectInstance) then
		getPrimaryKey((model::DomainObjectInstance) eContainer);

getPrimaryKey(model::wires::NewInstanceWire this) :
	getPrimaryKey(from);

getPrimaryKey(model::DomainObject this) :
	attributes.selectFirst( e | e.primaryKey );

getPrimaryKey(model::DomainAttribute this) :
	getPrimaryKey(eContainer);

getPrimaryKey(model::DomainObjectInstance this) :
	getPrimaryKey(inEdges.typeSelect(model::wires::NewInstanceWire).first());

getPrimaryKey(emf::EObject this) :
	null;

/** 
 * a hack way to create backtrace-able errors
 * based on http://www.openarchitectureware.org/forum/viewtopic.php?showtopic=5540
 * to use: «EXPAND exception FOR throwException("Your message goes here")»
 * AND add this anywhere in your template file: (very important)

«DEFINE exception FOR Object»
«ENDDEFINE»
   
 */
String throwException(String message) :
	JAVA org.openiaml.model.codegen.oaw.OawCodeGenerator.throwException(java.lang.String);

String _instrument(String destination, String filename, String location) :
	JAVA org.openiaml.model.codegen.oaw.coverage.InstrumentOawCode.instrument(java.lang.String, java.lang.String, java.lang.String);
