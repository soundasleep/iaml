package org.openiaml.model.drools

#list any import classes here.
import org.eclipse.emf.ecore.*;
import org.openiaml.model.inference.*;
import org.openiaml.model.model.*;
import org.openiaml.model.model.impl.*;
import org.openiaml.model.model.wires.*;
import org.openiaml.model.model.visual.*;
import org.openiaml.model.model.operations.*;
import org.openiaml.model.model.domain.*;
import ca.ecliptical.emf.xpath.*;
import java.util.*;

#declare any global variables here
global EcoreCreateElementsHelper handler;

# TODO can we put this function into something global?
function boolean connects(WireEdge wire, Object a, Object b) {
	return (wire.getFrom().equals(a) && wire.getTo().equals(b)) ||
		(wire.getFrom().equals(b) && wire.getTo().equals(a));
}

rule "Connect ConditionWires to RunInstanceWires created by SyncWires"
	when
		# we have a sync wire
		sw : SyncWire(overridden == false)
		source : ContainsOperations( ) from sw.to
		target : ContainsEventTriggers( ) from sw.from  
		
		# which has a run wire between event and operation
		event : EventTrigger( eContainer == source, name=="edit" )
		operation : Operation( eContainer == target, name=="update" )		
		run : RunInstanceWire( eContainer == sw, from == event, to == operation, name == "run" )
		
		# and the sync wire had a condition
		condition : Condition ( )
		sourceCw : ConditionWire( from == condition, to == sw )
		
		# but our run wire doesn't 
		not (ConditionWire (generatedBy == sourceCw, from == condition, to == run )) 
		
	then
		# copy the condition
		ConditionWire cw = handler.generatedConditionWire( sourceCw, sourceCw, condition, run );
		cw.setName("copied condition: " + sourceCw.getId());
		insert(cw);

end

rule "Connect ConditionWires to RunInstanceWires created by SyncWires (reverse)"
	when
		# we have a sync wire
		sw : SyncWire(overridden == false)
		source : ContainsEventTriggers( ) from sw.from  
		target : ContainsOperations( ) from sw.to
		
		# which has a run wire between event and operation
		event : EventTrigger( eContainer == source, name=="edit" )
		operation : Operation( eContainer == target, name=="update" )
		run : RunInstanceWire( eContainer == sw, from == event, to == operation, name == "run" )
		
		# and the sync wire had a condition
		condition : Condition ( )
		sourceCw : ConditionWire( from == condition, to == sw )
		
		# but our run wire doesn't 
		not (ConditionWire (generatedBy == sourceCw, from == condition, to == run )) 
		
	then
		# copy the condition
		ConditionWire cw = handler.generatedConditionWire( sourceCw, sourceCw, condition, run );
		cw.setName("copied condition [reverse]: " + sourceCw.getId());
		insert(cw);

end


rule "Connect ParameterWires to ConditionWires connected to RunInstanceWires created by SyncWires"
	when
		# we have a syncwire
		sw : SyncWire(overridden == false)
		target : ContainsEventTriggers( ) from sw.from  
		source : ContainsOperations( ) from sw.to
		
		# that has created a RunInstanceWire
		event : EventTrigger( eContainer == source, name=="edit" )
		operation : Operation( eContainer == target, name=="update" )
		run : RunInstanceWire( eContainer == sw, from == event, to == operation, name == "run" )

		# and this SyncWire has a condition
		condition : Condition ( )
		sourceCw : ConditionWire( from == condition, to == sw )
		
		# and the RunInstanceWire has the same condition 
		targetCw : ConditionWire (generatedBy == sourceCw, from == condition, to == run ) 
		
		# and the SyncWire had a parameter
		param : WireEdgesSource ( )
		paramWire : ParameterWire( from == param, to == sourceCw )

		# but this wire does not have it
		not (ParameterWire (generatedBy == sourceCw, from == param, to == targetCw ))
		
	then
		ParameterWire pw = handler.generatedParameterWire( sourceCw, sourceCw, param, targetCw );
		//cw.setName("copied parameter: " + paramWire.getName());
		pw.setName("[conditions] sync from: " + paramWire.getId());
		insert(pw);

end

rule "Connect ParameterWires to ConditionWires connected to RunInstanceWires created by SyncWires (reverse)"
	when
		# we have a syncwire
		sw : SyncWire(overridden == false)
		source : ContainsEventTriggers( ) from sw.from  
		target : ContainsOperations( ) from sw.to
		
		# that has created a RunInstanceWire
		event : EventTrigger( eContainer == source, name=="edit" )
		operation : Operation( eContainer == target, name=="update" )	
		run : RunInstanceWire( eContainer == sw, from == event, to == operation, name == "run" )
		
		# and this SyncWire has a condition
		condition : Condition ( )
		sourceCw : ConditionWire( from == condition, to == sw )
		
		# and the RunInstanceWire has the same condition 
		targetCw : ConditionWire (generatedBy == sourceCw, from == condition, to == run ) 
		
		# and the SyncWire had a parameter
		param : WireEdgesSource ( )
		paramWire : ParameterWire( from == param, to == sourceCw )
		
		# but this wire does not have it
		not (ParameterWire (generatedBy == sourceCw, from == param, to == targetCw ))
		
	then
		ParameterWire pw = handler.generatedParameterWire( sourceCw, sourceCw, param, targetCw );
		//cw.setName("copied parameter: " + paramWire.getName());
		pw.setName("[conditions] sync from [reverse]: " + paramWire.getId());
		insert(pw);

end
